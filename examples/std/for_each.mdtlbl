#**
* 整活性质的利用现有工具制作一个看起来很舒适的实用功能for循环
* 关于尝试将bang伪装的很先进
*#
const For = (const match @ {
    *I [in] _ *Stop F {
        setres For[I in 0 .. Stop .. 1 F];
    }
    *I [in] *Start _ *Stop F {
        setres For[I in Start .. Stop .. 1 F];
    }
    *I [in] *Start _ _ *Stop F {
        setres For[I in Start .. .. Stop .. 1 F];
    }
    *I [in] *Start _ *Stop _ *Step F {
        setres For[I in_impl Start .. Stop .. 1 F (?I < Stop)];
    }
    *I [in] *Start _ _ *Stop _ *Step F {
        setres For[I in_impl Start .. Stop .. 1 F (?I <= Stop)];
    }
    *I [inrev] _ *Stop F {
        setres For[I inrev 0 .. Stop .. 1 F];
    }
    *I [inrev] *Start _ *Stop F {
        setres For[I inrev Start .. Stop .. 1 F];
    }
    *I [inrev] *Start _ _ *Stop F {
        setres For[I inrev Start .. .. Stop .. 1 F];
    }
    *I [inrev] *Start _ *Stop _ *Step F {
        setres For[I inrev_impl Start .. Stop .. 1 F (?I > Stop)];
    }
    *I [inrev] *Start _ _ *Stop _ *Step F {
        setres For[I inrev_impl Start .. Stop .. 1 F (?I >= Stop)];
    }
    *I [in_impl] *Start _ *Stop _ *Step F C {
        setres For[I in_impl Start .. Stop .. 1 F C (_0 += _1;)];
    }
    *I [inrev_impl] *Start _ *Stop _ *Step F C {
        setres For[I in_impl Start .. Stop .. 1 F C (_0 -= _1;)];
    }
    *I [in_impl] *Start _ *Stop _ *Step F C Inc {
        I = Start;
        {
            const I = Start;
            match Builtin.EvalNum[C] {
                [`__` 0] { goto :end !C; }
                [1] {}
            }
        }
        do {
            setres F[I];
            Inc! I Step;
        } while C;
        :end
    }
    *I [of] @ F {
        inline@ Arg {{
            take Builtin.Const[I `Arg`];
            take F[];
        }}
    }
    *I [as] _ *Stop F {
        setres For[I as 0 .. Stop .. 1 F];
    }
    *I [as] *Start _ *Stop F {
        setres For[I as Start .. Stop .. 1 F];
    }
    *I [as] *Start _ _ *Stop F {
        setres For[I as Start .. .. Stop .. 1 F];
    }
    *I [as] *Start _ *Stop _ *Step F {
        setres For[I as_impl Start .. Stop .. 1 F (?J < Stop)];
    }
    *I [as] *Start _ _ *Stop _ *Step F {
        setres For[I as_impl Start .. Stop .. 1 F (?J <= Stop)];
    }
    *I [asrev] _ *Stop F {
        setres For[I asrev 0 .. Stop .. 1 F];
    }
    *I [asrev] *Start _ *Stop F {
        setres For[I asrev Start .. Stop .. 1 F];
    }
    *I [asrev] *Start _ _ *Stop F {
        setres For[I asrev Start .. .. Stop .. 1 F];
    }
    *I [asrev] *Start _ *Stop _ *Step F {
        setres For[I asrev_impl Start .. Stop .. 1 F (?J > Stop)];
    }
    *I [asrev] *Start _ _ *Stop _ *Step F {
        setres For[I asrev_impl Start .. Stop .. 1 F (?J >= Stop)];
    }
    *I [as_impl] *Start _ *Stop _ *Step F C {
        setres For[I as_impl Start .. Stop .. 1 F C (?_0 + _1)];
    }
    *I [asrev_impl] *Start _ *Stop _ *Step F C {
        setres For[I as_impl Start .. Stop .. 1 F C (?_0 - _1)];
    }
    *I [as_impl] *Start _ *Stop _ *Step F C Inc {
        match Builtin.EvalNum[(?Start+Step+Stop)] {
            [`__`] {
                match "for-as: can not eval ("Start".."Stop".."Step")" => @ {}
                take E = ""; inline@ S {
                    take E = Builtin.Concat[E *Builtin.Stringify[S]];
                }
                Builtin.Err! E;
                Builtin.Exit! 2;
            }
            _ {}
        }
        match Builtin.EvalNum[(?Step <= 0)] {
            [0] {}
            [1] {
                Builtin.Err! *Builtin.Concat[
                    "for-as: invalid step "
                    *Builtin.Stringify[Step]
                ];
                Builtin.Exit! 2;
            }
        }
        take J = Start;
        inline 0@ { match Builtin.EvalNum[C] {
            [1] {
                {
                    Builtin.Const! I `J`;
                    take F[];
                }
                take J = Inc[J Step];
            }
            [0] { Builtin.StopRepeat!; }
        } }
    }
});

For! i in 1..@links (
    print i;
);
print "---";
For! i in 1..6 ( # 不用检测头部
    print i;
);
print "---";
For! `I` as 1..4 ( # 循环展开
    print I;
);
print "---";
For! `I` of @copper @lead ( # 参数展开
    print I;
);
# NOTE: 如果在进行多次展开时有定义标签别忘了使用 Consted-DExp, 不然会重复定义
# 循环语句if语句什么的也是定义标签和跳转的语法糖, 也包括在内

#* >>>
set i 1
jump 5 greaterThanEq 1 @links
print i
op add i i 1
jump 2 lessThan i @links
print "---"
set i 1
print i
op add i i 1
jump 7 lessThan i 6
print "---"
print 1
print 2
print 3
print "---"
print @copper
print @lead
*#
