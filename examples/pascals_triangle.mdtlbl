Builtin.MissesMatch! 1;
Builtin.BindSep! '.';

# pre define macros
const For = (const match @ {
    *I [in] _ *Stop F {
        setres For[I in 0 .. Stop .. 1 F];
    }
    *I [in] *Start _ *Stop F {
        setres For[I in Start .. Stop .. 1 F];
    }
    *I [in] *Start _ _ *Stop F {
        setres For[I in Start .. .. Stop .. 1 F];
    }
    *I [in] *Start _ *Stop _ *Step F {
        setres For[I incmp Start .. Stop .. 1 F (?I < Stop)];
    }
    *I [in] *Start _ _ *Stop _ *Step F {
        setres For[I incmp Start .. Stop .. 1 F (?I <= Stop)];
    }
    *I [incmp] *Start _ *Stop _ *Step F C {
        I = Start;
        {
            const I = Start;
            match Builtin.EvalNum[C] {
                [`__` 0] { goto :end !C; }
                [1] {}
            }
        }
        do {
            setres F[I];
            I += Step;
        } while C;
        :end
    }
    *I [of] @ F {
        inline@ Arg {{
            take Builtin.Const[I `Arg`];
            take F[];
        }}
    }
    *I [as] _ *Stop F {
        setres For[I as 0 .. Stop .. 1 F];
    }
    *I [as] *Start _ *Stop F {
        setres For[I as Start .. Stop .. 1 F];
    }
    *I [as] *Start _ _ *Stop F {
        setres For[I as Start .. .. Stop .. 1 F];
    }
    *I [as] *Start _ *Stop _ *Step F {
        setres For[I ascmp Start .. Stop .. 1 F (?J < Stop)];
    }
    *I [as] *Start _ _ *Stop _ *Step F {
        setres For[I ascmp Start .. Stop .. 1 F (?J <= Stop)];
    }
    *I [ascmp] *Start _ *Stop _ *Step F C {
        match Builtin.EvalNum[(?Start+Step+Stop)] {
            [`__`] {
                match "for-as: can not eval ("Start".."Stop".."Step")" => @ {}
                take E = ""; inline@ S {
                    take E = Builtin.Concat[E *Builtin.Stringify[S]];
                }
                Builtin.Err! E;
                Builtin.Exit! 2;
            }
            _ {}
        }
        match Builtin.EvalNum[(?Step <= 0)] {
            [0] {}
            [1] {
                Builtin.Err! *Builtin.Concat[
                    "for-as: invalid step "
                    *Builtin.Stringify[Step]
                ];
                Builtin.Exit! 2;
            }
        }
        take J = Start;
        inline 0@ { match Builtin.EvalNum[C] {
            [1] {
                {
                    Builtin.Const! I `J`;
                    take F[];
                }
                take*J = J + Step;
            }
            [0] { Builtin.StopRepeat!; }
        } }
    }
});
const QuickMemory = (match @ {
    Mem {
        setres Mem;
        const $.F = (match @ {
            #**
            * extend methods
            *#
            {
                const $.Read = ([H:..](match @ {
                    Result Addr {
                        setres Result;
                        read Result H Addr;
                    }
                }));
            }
            #**
            * quick read
            *#
            Addr {
                read $ .. Addr;
            }
            #**
            * write
            *#
            @ Value {
                inline@ Addr {
                    write Value .. Addr;
                }
            }
        });
    }
});

# codes
const TriangleSize = 10;
const CurLine = QuickMemory[cell1]->F;
const PrevLine = QuickMemory[cell2]->F;

print"\
1
1 1
";

lastSize = 2;

PrevLine! 0 1 1; # like `PrevLine[0, 1] = 1`

For! i in 3....TriangleSize (
    CurLine! 0 1;

    For! j in 1..lastSize (
        CurLine! j (*PrevLine[(*j- 1)] + PrevLine[j]);
    );
    CurLine! lastSize 1;
    lastSize++;

    For! c in ..lastSize (
        print CurLine[].Read[cur_c c];
        PrevLine! c cur_c;
        if c < (*lastSize- 1) { print " "; }
    );
    print "\n";
);

printflush message1;

#* >>>
print "1\n1 1\n"
set lastSize 2
write 1 cell2 0
write 1 cell2 1
set i 3
write 1 cell1 0
set j 1
jump 15 greaterThanEq 1 lastSize
op sub __85 j 1
read __84 cell2 __85
read __87 cell2 j
op add __82 __84 __87
write __82 cell1 j
op add j j 1
jump 8 lessThan j lastSize
write 1 cell1 lastSize
op add lastSize lastSize 1
set c 0
jump 27 greaterThanEq 0 lastSize
read cur_c cell1 c
print cur_c
write cur_c cell2 c
op sub __133 lastSize 1
jump 25 greaterThanEq c __133
print " "
op add c c 1
jump 19 lessThan c lastSize
print "\n"
op add i i 1
jump 5 lessThanEq i 10
printflush message1
*#
