#*
 * 这是提供的一个示例程序
 * 这是一个插入排序算法
 *#

do { # 按钮弹起时等待按钮被按下
    wait 0.1;
} while (sensor _0 switch1 @enabled;);

read length cell1 0; # 被排序的数组长
set i 1;
while i < length {
    read num bank1 i;
    set j i;
    while (j: set c j; op j j - 1;) >= 0 {
        read num_1 bank1 j;
        goto :break1 num_1 <= num;
        write num_1 bank1 c;
    } :break1 # 跳出循环用的标记
    write num bank1 c;
    op i i + 1;
}
control enabled switch1 true 0 0 0;

#* 以上程序会生成的代码
wait 0.1
sensor _0 switch1 @enabled
jump 0 notEqual _0 false
read length cell1 0
set i 1
jump 20 greaterThanEq i length
read num bank1 i
set j i
set c j
op sub j j 1
jump 17 lessThan j 0
read num_1 bank1 j
jump 17 lessThanEq num_1 num
write num_1 bank1 c
set c j
op sub j j 1
jump 11 greaterThanEq j 0
write num bank1 c
op add i i 1
jump 6 lessThan i length
control enabled switch1 true 0 0 0
*#
